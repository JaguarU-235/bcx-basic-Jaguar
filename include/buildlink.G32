'
' Build a linkfile for RB+ ROM
' (or just convert assets)
' 22-Feb-2015 Reboot
' Run it like:
' build link <path+filename of assets file> <base project dir>
'

Mode StrSpace 0

Global Const __argmax = 50
Global __argc As Int
Global __argv() As String
ConvertCMDLine

Dim basedir$ = __argv(2)

Open basedir$ + "\build\build.log" for Append As # 10

If Not Exist(__argv(1))
  Print # 10; "Buildlink aborting - " + __argv(1) + " file not found"
  Open basedir$ + "\build\romassets.inc" for Output As # 3 : Close # 3
  Open basedir$ + "\build\romassets.h" for Output As # 4 : Close # 4
  Open basedir$ + "\build\ramassets.inc" for Output As # 6 : Close # 6
  'Open basedir$ + "\build\linkfile.bin" for Output As # 2 : Close # 2
  If Exist(basedir$ + "\build\linkfile.bin")
    DeleteFile Silent, Yes basedir$ + "\build\linkfile.bin"
  EndIf
  Quit 1
EndIf

Dim any_rom_asset As Bool = False
Dim base_adr% = $802000 + 196 'rom base address + header + copy routine size (hardcoded for now)
Dim ass_adr% = base_adr%
Dim tempbuf(4 * 1024 * 1024) As Byte
Dim tempadr As Int = V:tempbuf(0)
Dim fullnameslash$
Dim packext$

Print # 10; "Buildlink starting"
Dim romabs$, varname$, type$, assetfile$
Print # 10; "    Opening - " + __argv(1) + "..."
Open __argv(1) for Input As # 1
Print # 10; "    Opening - " + basedir$ + "\build\linkfile.bin" + "..."
Open basedir$ + "\build\linkfile.bin" for Output As # 2
Print # 10; "    Opening - " + basedir$ + "\build\romassets.inc" + "..."
Open basedir$ + "\build\romassets.inc" for Output As # 3
Print # 10; "    Opening - " + basedir$ + "\build\romassets.h" + "..."
Open basedir$ + "\build\romassets.h" for Output As # 4
Print # 10; "    Opening - " + basedir$ + "\build\ramassets.inc" + "..."
Open basedir$ + "\build\ramassets.inc" for Output As # 6
Print # 10; "    Opening - " + basedir$ + "\build\build.log" + "..."
Print # 6; ".dphrase"
Dim size As Int
Dim fullname$
Dim plainfile$
Dim i As Int

Dim filepointer As Int
Dim rate As Int, bpp As Int

Dim pad$ = "PUSH THE BUTTON!"
While Not EOF(# 1)
  Print # 10; "    Reading assets.txt line"
  filepointer = Seek(# 1)
  Line Input # 1, romabs$
  romabs$ = Left$(romabs$, 1)
  If romabs$ = ";" Or romabs$ = "#" Or romabs$ = "'" Or romabs$ = "*" Or romabs$ = ""
    'Print "Skipping line"
    GoTo skipline
  EndIf
  Seek # 1, filepointer
  Input # 1, romabs$, varname$, type$, assetfile$
  romabs$ = Trim$(romabs$)
  varname$ = Trim$(varname$)
  type$ = Trim$(type$)
  assetfile$ = Trim$(assetfile$)

  Print # 10; "        Processing " + assetfile$
  'Align to 16 bytes, just cause I say so :P
  BPut # 2, V:pad$, (16 - (ass_adr% And $f)) And $f
  ass_adr% = (ass_adr% + 15) And $fffffff0
  fullname$ = basedir$ + "\" + assetfile$
  fullnameslash$ = assetfile$
  plainfile$ = SubStr(assetfile$, RInStr(assetfile$, "\") + 1)

  If Not Exist(fullname$)
    Print # 10; "Buildlink aborting - Couldn't find file " + fullname$
    Close # 2
    DeleteFile Silent, Yes basedir$ + "\build\linkfile.bin"
    'Message "Aborting - Couldn't find file " + fullname$
    Quit 2
  EndIf
  For i = 1 To Len(fullnameslash$)
    If Mid$(fullnameslash$, i, 1) = "\"
      Mid$(fullnameslash$, i, 1) = "/"
    EndIf
  Next i
  If Left$(Lower$(type$), 8) = "gfx_clut"
    bpp = Val(SubStr(type$, 9))
    Print # 10; "        Converting BMP internally into gfxdata/clut..."
    convertbmp(fullname$, True)
    packext$ = packfile$(basedir$ + "\build\" + plainfile$ + ".gfxdata")
    If Upper$(romabs$) = "ROM"
      Print # 10; "        Adding to the linkfile as ROM..."
      any_rom_asset = True
      Print # 3; varname$; " EQU $"; Hex$(ass_adr%)
      Print # 4; "#define "; varname$; " 0x"; Hex$(ass_adr%)
      Open basedir$ + "\build\" + plainfile$ + ".gfxdata" + packext$ for Input As # 5
      size = LOF(# 5)
      BGet # 5, tempadr, size
      BPut # 2, tempadr, size
      Close # 5
      Add ass_adr%, size

      'Align to 16 bytes, just cause I say so :P
      BPut # 2, V:pad$, (16 - (ass_adr% And $f)) And $f
      ass_adr% = (ass_adr% + 15) And $fffffff0

      If bpp < 16
        Print # 3; varname$; "_clut EQU $"; Hex$(ass_adr%)
        Print # 4; "#define "; varname$; "_clut 0x"; Hex$(ass_adr%)
        Open basedir$ + "\build\" + plainfile$ + ".clut" for Input As # 5
        size = LOF(# 5)
        BGet # 5, tempadr, size
        BPut # 2, tempadr, size
        Close # 5
        Add ass_adr%, size
      EndIf
    Else
      Print # 10; "        Adding in RAM..."
      Print # 6; varname$; ": incbin ""build/" + plainfile$ + ".gfxdata" + packext$ + """"
      Print # 6; varname$; "_end:"
      If bpp < 16
        Print # 6; varname$; "_clut: incbin ""build/" + plainfile$ + ".clut" + """"
        Print # 6; varname$; "_clut_end:"
      EndIf
      Print # 6; ".extern " + varname$
      If bpp < 16
        Print # 6; ".extern " + varname$ + "_clut"
      EndIf
      Print # 6; ".dphrase"
      Print # 4; "extern void *" + varname$ + " asm(""" + varname$ + """);"
      If bpp < 16
        Print # 4; "extern void *" + varname$ + "_clut" + " asm(""" + varname$ + "_clut"");"
      EndIf
    EndIf
  ElseIf Left$(Lower$(type$), 10) = "gfx_noclut"
    bpp = Val(SubStr(type$, 11))
    Print # 10; "        Converting BMP internally into gfxdata..."
    convertbmp(fullname$, False)
    packext$ = packfile$(basedir$ + "\build\" + plainfile$ + ".gfxdata")
    If Upper$(romabs$) = "ROM"
      Print # 10; "        Adding to the linkfile as ROM..."
      any_rom_asset = True
      Print # 3; varname$; " EQU $"; Hex$(ass_adr%)
      Print # 4; "#define "; varname$; " 0x"; Hex$(ass_adr%)
      Open basedir$ + "\build\" + plainfile$ + ".gfxdata" + packext$ for Input As # 5
      size = LOF(# 5)
      BGet # 5, tempadr, size
      BPut # 2, tempadr, size
      Close # 5
      Add ass_adr%, size
      Print # 3; varname$; "_end EQU $"; Hex$(ass_adr%)
      Print # 4; "#define "; varname$; "_end 0x"; Hex$(ass_adr%)
    Else
      Print # 10; "        Adding in RAM..."
      Print # 6; varname$; ": incbin ""build/" + plainfile$ + ".gfxdata" + packext$ + """"
      Print # 6; varname$; "_end:"
      Print # 6; ".extern " + varname$
      Print # 6; ".extern " + varname$ + "_end"
      Print # 6; ".dphrase"
      Print # 4; "extern void *" + varname$ + " asm(""" + varname$ + """);"
      Print # 4; "extern void *" + varname$ + "_end" + " asm(""" + varname$ + "_end"");"
    EndIf
  ElseIf Left$(Lower$(type$), 7) = "gfx_cry"
    Print # 10; "        Converting bmp into CRY (using bmp_cry_nodither)..."
    Print # 10; "        Command run: bmp_cry_nodither.exe  """ + fullname$ + """ " + basedir$ + "\build\" + plainfile$ + ".cry"
    System "bmp_cry_nodither.exe """ + fullname$ + """  " + basedir$ + "\build\" + plainfile$ + ".cry""", Wait, Show SW_HIDE
    packext$ = packfile$(basedir$ + "\build\" + plainfile$ + ".cry")
    If Upper$(romabs$) = "ROM"
      Print # 10; "        Adding to the linkfile as ROM..."
      any_rom_asset = True
      Print # 3; varname$; " EQU $"; Hex$(ass_adr%)
      Print # 4; "#define "; varname$; " 0x"; Hex$(ass_adr%)
      Open basedir$ + "\build\" + plainfile$ + ".cry" + packext$ for Input As # 5
      size = LOF(# 5)
      BGet # 5, tempadr, size
      BPut # 2, tempadr, size
      Close # 5
      Add ass_adr%, size
      Print # 3; varname$; "_end EQU $"; Hex$(ass_adr%)
      Print # 4; "#define "; varname$; "_end 0x"; Hex$(ass_adr%)
    Else
      Print # 10; "        Adding in RAM..."
      Print # 6; varname$; ": incbin ""build/" + plainfile$ + ".cry" + packext$ + """"
      Print # 6; varname$; "_end:"
      Print # 6; ".extern " + varname$
      Print # 6; ".extern " + varname$ + "_end"
      Print # 6; ".dphrase"
      Print # 4; "extern void *" + varname$ + " asm(""" + varname$ + """);"
      Print # 4; "extern void *" + varname$ + "_end" + " asm(""" + varname$ + "_end"");"
    EndIf
  ElseIf Left$(Lower$(type$), 7) = "sfx_raw"
    Print # 10; "        Converting sound file into raw file (using sox)..."
    rate = Val(SubStr(type$, 8))
    If rate < 1024
      rate = 8000
    EndIf

    If checkdate(fullname$, basedir$ + "\build\" + plainfile$ + ".raw")
      Print # 10; "        Command run: sox.exe " + fullname$ + "--channels 1 --bits 8 --encoding signed-integer """ + basedir$ + "\build\" + plainfile$ + ".raw"" --show-progress rate -v " + Str$(rate)
      System "sox.exe """ + fullname$ + """ --channels 1 --bits 8 --encoding signed-integer """ + basedir$ + "\build\" + plainfile$ + ".raw"" --show-progress rate -v " + Str$(rate), Wait, Show SW_HIDE
    Else
      Print # 10; "        Destination file " + basedir$ + "\build\" + plainfile$ + ".raw already exists and is newer than the source file - skipping"
    EndIf
    packext$ = packfile$(basedir$ + "\build\" + plainfile$ + ".raw")
    If Upper$(romabs$) = "ROM"
      Print # 10; "        Adding to the linkfile as ROM..."
      any_rom_asset = True
      Print # 3; varname$; " EQU $"; Hex$(ass_adr%)
      Print # 4; "#define "; varname$; " 0x"; Hex$(ass_adr%)
      Open basedir$ + "\build\" + plainfile$ + ".raw" + packext$ for Input As # 5
      size = LOF(# 5)
      BGet # 5, tempadr, size
      BPut # 2, tempadr, size
      Close # 5
      Add ass_adr%, size
      Print # 3; varname$; "_end EQU $"; Hex$(ass_adr%)
      Print # 4; "#define "; varname$; "_end 0x"; Hex$(ass_adr%)
    Else
      Print # 10; "        Adding in RAM..."
      Print # 6; varname$; ": incbin ""build/" + plainfile$ + ".raw" + packext$ + """"
      Print # 6; varname$; "_end:"
      Print # 6; ".extern " + varname$
      Print # 6; ".extern " + varname$ + "_end"
      Print # 6; ".dphrase"
      Print # 4; "extern void *" + varname$ + " asm(""" + varname$ + """);"
      Print # 4; "extern void *" + varname$ + "_end" + " asm(""" + varname$ + "_end"");"
    EndIf
  ElseIf Lower$(Left$(type$, 8)) = "sfx_mlaw"
    Print # 10; "        Converting sound file into m-law file (using sox)..."
    rate = Val(SubStr(type$, 9))
    If rate < 1024
      rate = 8000
    EndIf

    If checkdate(fullname$, basedir$ + "\build\" + plainfile$ + ".ul")
      Print # 10; "        Command run: sox.exe " + fullname$ + """ --channels 1 --bits 8 --encoding mu-law """ + basedir$ + "\build\" + plainfile$ + ".ul"" --show-progress rate -v " + Str$(rate)
      System "sox.exe """ + fullname$ + """ --channels 1 --bits 8 --encoding mu-law """ + basedir$ + "\build\" + plainfile$ + ".ul"" --show-progress rate -v " + Str$(rate), Wait, Show SW_HIDE
    Else
      Print # 10; "        Destination file "; basedir$ + "\build\" + plainfile$ + ".ul already exists and is newer than the source file - skipping"
    EndIf

    packext$ = packfile$(basedir$ + "\build\" + plainfile$ + ".ul")

    If Upper$(romabs$) = "ROM"
      Print # 10; "        Adding to the linkfile as ROM..."
      any_rom_asset = True
      Print # 3; varname$; " EQU $"; Hex$(ass_adr%)
      Print # 4; "#define "; varname$; " 0x"; Hex$(ass_adr%)
      Open basedir$ + "\build\" + plainfile$ + ".ul" + packext$ for Input As # 5
      size = LOF(# 5)
      BGet # 5, tempadr, size
      BPut # 2, tempadr, size
      Close # 5
      Add ass_adr%, size
      Print # 3; varname$; "_end EQU $"; Hex$(ass_adr%)
      Print # 4; "#define "; varname$; "_end 0x"; Hex$(ass_adr%)
    Else
      Print # 10; "        Adding in RAM..."
      Print # 6; varname$; ": incbin ""build/" + plainfile$ + ".ul" + packext$ + """"
      Print # 6; varname$; "_end:"
      Print # 6; ".extern " + varname$
      Print # 6; ".extern " + varname$ + "_end"
      Print # 6; ".dphrase"
      Print # 4; "extern void *" + varname$ + " asm(""" + varname$ + """);"
      Print # 4; "extern void *" + varname$ + "_end" + " asm(""" + varname$ + "_end"");"
    EndIf
  ElseIf Lower$(Left$(type$, 7)) = "rmotion"
    Print # 10; "        Executing command: rmotion.exe " + fullname$ + " " + basedir$ + "\build\" + plainfile$ + ".s"
    System "rmotion.exe " + fullname$ + " " + basedir$ + "\build\" + plainfile$ + ".s", Wait, Show SW_HIDE
    Print # 10; "        Adding in RAM..."
    Print # 6; " include ""build/" + fullnameslash$  + ".s"""
    Open fullname$ for Input As # 5
    Open basedir$ + "\build\" + plainfile$ + ".s" for Append As # 7
    Repeat
      Line Input # 5, romabs$
      i = InStr(romabs$, ":")
      If i <> 0 And Left$(romabs$, 1) <> "."
        Print # 4; "extern int *" + Left$(romabs$, i - 1) + " asm(""" + Left$(romabs$, i - 1) + """);" " //Rmotion"
        Print # 7; ".extern " + Left$(romabs$, i - 1)
      EndIf
    Until EOF(# 5)
    Close # 5
    Close # 7
  Else
'##E#
    If Upper$(romabs$) = "ROM"
      Print # 10; "        Adding " + fullname$ + " to the linkfile as ROM..."
      any_rom_asset = True
      Print # 3; varname$; " EQU $"; Hex$(ass_adr%)
      Print # 4; "#define "; varname$; " 0x"; Hex$(ass_adr%)
      Open fullname$ for Input As # 5
      size = LOF(# 5)
      BGet # 5, tempadr, size
      BPut # 2, tempadr, size
      Close # 5
      Add ass_adr%, size
      Print # 3; varname$; "_end EQU $"; Hex$(ass_adr%)
      Print # 4; "#define "; varname$; "_end 0x"; Hex$(ass_adr%)
    Else
      Print # 10; "        Adding " + fullname$ + " in RAM..."
      Print # 6; varname$; ": incbin """ + fullnameslash$ + """"
      Print # 6; varname$; "_end:"
      Print # 6; ".extern " + varname$
      Print # 6; ".extern " + varname$ + "_end"
      Print # 6; ".dphrase"
      Print # 4; "extern void *" + varname$ + " asm(""" + varname$ + """);"
    EndIf
  EndIf
  skipline:
Wend
Close # 1
Close # 3
Close # 4
Close # 6
Close # 2
Print # 10; "Buildlink finished - everything went fine!"
If any_rom_asset = False
  DeleteFile Silent, Yes basedir$ + "\build\linkfile.bin"
EndIf
'Repeat
'Until InKey$ <> ""

Quit 0


'
' Convert a 8bpp bmp to jag format
' Code by promethea, with original 68k code by Sinister developments
'
' Foooooked in the arse by ggn in 27-02-2013 (WARNING - only 256c works proppa for now!)
' Mono mode added by ggn in 15-02-2015 for Kevvykevkevkevkev
' 4bit converter fixed in 22-02-2015
'
' Needs to be launched with parameters from the command prompt:
' convbmp <input filename> <output filename> <colour bit depth> <width in bytes> <height in scanlines>
' It will output <filename>.jag
'
>Procedure convertbmp(file$, dumpclut As Bool)
  Dim infile$, outfile$
  Dim depth, width, height, l, i, x As Int
  Dim sourceaddr, xBGR As Int
  Dim r As Byte, g As Byte, b As Byte

  infile$ = file$
  outfile$ = basedir$ + "\build\" + plainfile$

  '
  ' Read in source file
  '
  Open file$ for Binary As # 5
  l = LOF(# 5)
  Dim sourcebmp(l) As Byte
  sourceaddr = V:sourcebmp(0)
  Dim aaa As Byte
  For i = 1 To l
    BGet # 5, V:aaa, 1
    sourcebmp(i - 1) = aaa
  Next i
  Close # 5

  '
  ' Figure out width and height
  '
  width = sourcebmp($15) * $100000 + sourcebmp($14) * $1000 + sourcebmp($13) * $100 + sourcebmp($12)
  height = sourcebmp($19) * $100000 + sourcebmp($18) * $1000 + sourcebmp($17) * $100 + sourcebmp($16)
  depth = sourcebmp($1d) * $100 + sourcebmp($1c)
  If depth = 24 'special case, user defines bpp. otherwise assume 16
    If bpp = 24
      depth = 24
    Else
      depth = 16
    EndIf
  EndIf
  Print # 10; "        File is "; Str$(width); " pixels wide, "; Str$(height); " pixels high and is "; Str$(depth); " bits/pixel"

  '
  ' Convert palette to jag format
  '
  If depth < 16
    If sourcebmp($1c) <> 4
      xBGR = 53
    Else
      xBGR = $35
    EndIf
    Dim palette(255) As Word
    Dim red, green, blue As Word
    For i = 0 To (2 ^ depth) - 1
      blue = Rol&((Shr&(sourcebmp(xBGR + 1), 3)), 6)
      green = Shr&(sourcebmp(xBGR + 2), 2)
      red = Ror&(Shr&(sourcebmp(xBGR + 3), 3), 5)
      palette(i) = Ror&(red Or green Or blue, 8)            'Fuck you endianess!
      Add xBGR, 4
    Next i
  EndIf

  '
  ' Do the conversion
  '
  Dim screenstart, destindex As Int
  Dim col As Byte
  Select depth
  Case -1                         '***************** just dump the palette & exit
    Open file$ for Binary As # 5
    For i = 1 To 256
      BPut # 1, V:palette(i - 1), 2
    Next i
    Close # 5

  Case 1                          '***************** 1-bit
    Dim y As Int ', x As Int
    Dim b$
    Dim z As Picture
    Set z = LoadPicture(file$)
    Dim w As Int = Round(z.Width / 26.46)
    Dim h As Int = Round(z.Height / 26.46)
    Dim yy As Int = h


    If dumpclut
      Open outfile$ + ".clut" for Output As # 5 'clut for 1bpp? lol
      Close # 5
    EndIf

    If checkdate(file$, outfile$ + ".gfxdata")

      Open outfile$ + ".gfxdata" for Output As # 5

      screenstart = 62
      If width >= 32
        For i = height - 1 DownTo 0
          BPut # 5, V:sourcebmp(screenstart + i * (width / 8)), width / 8
        Next i
      Else
        For i = height - 1 DownTo 0
          BPut # 5, V:sourcebmp(screenstart + i * 4), width / 8
        Next i
      EndIf
      Close # 5
    Else
      'Print  "        Destination file already exists and is newer than the source file - skipping"
    EndIf
  Case 4                          '***************** 4-bit
    If checkdate(file$, outfile$ + ".gfxdata")
      width = width / 2
      screenstart = 54 + 64
      Dim outbuffer(width * height) As Byte
      destindex = width * (height - 1)
      For i = 1 To height
        For x = 0 To width - 1
          outbuffer(destindex + x) = sourcebmp(screenstart)
          Inc screenstart
        Next x
        Sub destindex, width
      Next i
      Open outfile$ + ".gfxdata" for Binary As # 5
      For i = 1 To width * height
        BPut # 5, V:outbuffer(i - 1), 1
      Next i
      Close # 5
    Else
      Print # 10; "        Destination file " + outfile$ + ".gfxdata already exists and is newer than the source file - skipping"
    EndIf
    If dumpclut
      If checkdate(file$, outfile$ + ".clut")
        Open outfile$ + ".clut" for Binary As # 5
        For i = 1 To 16
          BPut # 5, V:palette(i - 1), 2
        Next i
        Close # 5
      Else
        Print # 10; "        Destination file " + outfile$; ".clut already exists and is newer than the source file - skipping"
      EndIf
    EndIf
  Case 8                          '***************** 8-bit
    If checkdate(file$, outfile$ + ".gfxdata")
      screenstart = 1024 + 54
      Dim outbuffer(width * height) As Byte
      destindex = width * (height - 1)
      For i = 1 To height
        For x = 0 To width - 1
          outbuffer(destindex + x) = sourcebmp(screenstart)
          Inc screenstart
        Next x
        Sub destindex, width
      Next i
      If dumpclut
        If checkdate(file$, outfile$ + ".clut")
          BSave outfile$ + ".clut", V:palette(0), 512
        Else
          Print # 10; "        Destination file " + outfile$ + ".clut already exists and is newer than the source file - skipping"
        EndIf
      EndIf
      Open outfile$ + ".gfxdata" for Binary As # 5
      For i = 1 To width * height
        BPut # 5, V:outbuffer(i - 1), 1
      Next i
      Close # 5
    Else
      Print # 10; "        Destination file " + outfile$ + ".gfxdata already exists and is newer than the source file - skipping"
    EndIf
  Case 16                          '***************** 16-bit raw
    If checkdate(file$, outfile$ + ".gfxdata")
      screenstart = 54
      Dim outbuffer16(width * height) As Word
      destindex = width * (height - 1)
      For i = 1 To height
        For x = 0 To width - 1
          'Print screenstart
          r = Shr|(sourcebmp(screenstart + 2), 3)
          g = Shr|(sourcebmp(screenstart + 1), 2)
          b = Shr|(sourcebmp(screenstart + 0), 3)
          outbuffer16(destindex + x) = Ror&(Shl&(r, 11) + Shl&(b, 6) + g, 8)
          Add screenstart, 3
        Next x
        Sub destindex, width
      Next i
      Open outfile$ + ".gfxdata" for Binary As # 5
      BPut # 5, V:outbuffer16(0), 2 * width * height
      Close # 5
    Else
      Print # 10; "        Destination file " + outfile$ + ".gfxdata already exists and is newer than the source file - skipping"
    EndIf
  Case 24                          '***************** 24-bit raw
    If checkdate(file$, outfile$ + ".gfxdata")
      screenstart = 54
      Dim outbuffer24(width * height * 3) As Byte
      destindex = width * (height - 1) * 3
      For i = 1 To height
        For x = 1 To width
          'Print screenstart
          outbuffer24(destindex + x * 3 + 0) = r = sourcebmp(screenstart + 2)
          outbuffer24(destindex + x * 3 + 1) = g = sourcebmp(screenstart + 1)
          outbuffer24(destindex + x * 3 + 2) = b = sourcebmp(screenstart + 0)
          'outbuffer16(destindex + x) = Ror&(Shl&(r, 11) + Shl&(b, 6) + g, 8)
          Add screenstart, 3
        Next x
        Sub destindex, width * 3
      Next i
      Open outfile$ + ".gfxdata" for Binary As # 5
      BPut # 5, V:outbuffer16(0), 3 * width * height
      Close # 5
    Else
      Print # 10; "        Destination file " + outfile$ + ".gfxdata already exists and is newer than the source file - skipping"
    EndIf
  EndSelect
Return

Function packfile$(file$)
  If Right$(Lower$(type$), 5) = "_pack"
    If checkdate(file$, file$ + ".pack")
      Print # 10; "        Will attempt to pack " + file$
      Print # 10; "        Command run: pack2e.exe """ + file$ + """ """ + file$ + ".pack"""
      System "pack2e.exe """ + file$ + """ """ + file$ + ".pack""", Wait, Show SW_HIDE
      Return ".pack"
    Else
      Print # 10; "        Destination file " + file$ + ".pack already exists and is newer than the source file - skipping"
      Return ".pack"
    EndIf
  Else
    Return ""
  EndIf
EndFunction
>Function checkdate(a$, b$)
  If Not Exist(b$)
    Return True
  EndIf
  Return FileDateTime(a$) > FileDateTime(b$)
EndFunc
>Procedure ConvertCMDLine()
  // The global variable __argc holds the
  // number of commandline arguments after executing ConvertCMDLine().
  // Arguments are separated by space(s)

  // __argv() is an Array with the splitted arguments.
  // Only __argmax arguments are returned.

  // __argv(0) holds the complete path, filename included.

  Local i As Int = 0, j As Int = 0
  Local cmd$

  Local LargeArg As Boolean = False
  Local a$

  ReDim __argv(__argmax)
  __argv(0) = App.FileName

  // Remove quotes
  If Left$(__argv(0), 1) = #34
    __argv(0) = Mid(__argv(0), 2)
  EndIf
  If Right$(__argv(0), 1) = #34
    __argv(0) = Left(__argv(0), Len(__argv(0)) - 1)
  EndIf

  cmd$ = Trim(_DosCmd$) + #32
  If Left$(cmd$, 1) <> """"
    i = InStr(cmd$, #32)
  Else
    'Debug.Print InStr(cmd$, """", 2)
    i = InStr(cmd$, #34, 2) : LargeArg = True

    cmd$ = Mid$(cmd$, 2) // remove space at start
  EndIf

  While i > 0
    j++
    If LargeArg
      // remove space at end
      a$ = Left$(cmd$, i - 2)
      If Len(a)
        __argv(j) = Left$(cmd$, i - 2)
      Else
        j--
      EndIf
      LargeArg = False
    Else
      // only remove space at end
      a$ = Left$(cmd$, i - 1)
      If Len(a)
        __argv(j) = Left$(cmd$, i - 1)
      Else
        j--
      EndIf
    EndIf

    Exit If (i + 1) > Len(cmd$)

    cmd$ = Mid$(cmd$, i + 1)

    If Left$(cmd$, 1) <> """"
      i = InStr(cmd$, #32)
    Else
      i = InStr(cmd$, #34, 2) : LargeArg = True
      cmd$ = Mid$(cmd$, 2) // remove space at beginning
    EndIf
  Wend

  // Return number of arguments
  __argc = j

EndProcedure
$DatFile
$FormVersion = 2
$ExeName = ..\BIN\Buildlink.exe
$FileDescription = Description
$Comments = Author: Not registered   
$CompanyName = Not registered   
$LegalCopyright = © 2015 Not registered   
$LanguageId = 0409
$FileVersion = %d.%d.%d.%d
$ProductVersion = %d.%d.%d.%d
$DatFile

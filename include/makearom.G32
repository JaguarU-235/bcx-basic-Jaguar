'
' RB+ ROM builder
' (c) Reboot in 2015
' Argument 1: compiled .bin file from rln
' Argument 2: path+filename linkfile
' Argument 3: destination .rom file
' Argument 4: if it's equal to "unpacked" then it will not pack the rom
' Argument 5: if it's equal to "fastdepack" then the gpu depack code will be used during boot
' NOTE: when changing loader bin size also change hardcoded size in buildlink.g32
'
Mode StrSpace 0 'fucking VB :P

ConvertCMDLine()
'If __argc <> 3
'End
'EndIf

'If Exist(__argv(2))
'DeleteFile Yes, __argv(2)
'EndIf

Dim packed As Bool = True
Dim fastdepack As Bool = False
Dim rblen%
If __argv(4) = "unpacked"
  packed = False
EndIf

If __argv(5) = "fastdepack"
  fastdepack = True
EndIf

Open __argv(3) for Output As # 1

'Open "..\rbasic.bin" for Input As # 2
'Open "..\rbasic.rom" for Output As # 1

Dim logfile$ = __argv(1)
logfile$ = Left$(logfile$, RInStr(logfile$, "\"))
Open logfile$ + "build.log" for Append As # 10
Print # 10; "Make-a-rom starting"


Dim romlen% = 0
'
' First, dump the universal header
' (maybe this should be optional if someone wants to run it on an Alpine?
'  ...on second thoughts LOLOL)
'

Print # 10; "    Dumping universal header..."
Dim buffer(2 * 1024 * 1024) As Byte
Dim b% = V:buffer(0)
Open ":univ" for Input As # 3
BGet # 3, b%, 8192
Close # 3
BPut # 1, b%, 8192
Add romlen%, 8192

'
' Now dump the copy to RAM routine
'
Print # 10; "    Dumping startup code..."
Dim linkcopyrom%
If fastdepack
  Print # 10; "    (GPU depacking routine)"
  Open ":copyrom_gpu" for Input As # 3
Else
'##E#
  Print # 10; "    (CPU depacking routine)"
  Open ":copyrom" for Input As # 3
EndIf
linkcopyrom% = LOF(# 3)
BGet # 3, b%, linkcopyrom%
linkcopyrom% = (linkcopyrom% + 15) And $fffffff0 'round size up to multiples of 16
Close # 3

If Exist(__argv(2))
  Open __argv(2)  for Input As # 3 'open linkfile
  Dim linklen% = LOF(# 3) 'get linkfile's length
Else
  linklen% = 0
EndIf

lpoke_endian(b% + $e, $800000 + 8192 + linkcopyrom% + linklen%) '(rb+ binary address = rom start + universal header + copy routine + linkfile's length)
lpoke_endian(b% + $14, $4000) 'run address
If !packed
  Open __argv(1) for Input As # 5
  rblen% = LOF(# 5)
  lpoke_endian(b% + $1e, ((rblen% / 4) + 3) And $fffffffc) 'length of rb+ program + data in longwords, rounded up
  DPoke b% + $1a, $714e         ' Skip unpacking
EndIf

BPut # 1, b%, linkcopyrom%
Add romlen%, linkcopyrom%

'
' Read linkfile and dump it to rom
'

If linklen% > 0
  Print # 10; "    Dumping linkfile..."
  Dim linkbuff(linklen%) As Byte
  BGet # 3, V:linkbuff(0), linklen%
  BPut # 1, V:linkbuff(0), linklen%
  Close # 3
  Add romlen%, linklen%
Else
  Print # 10; "    Linkfile not found - assuming no assets in ROM..."
EndIf

If packed
  '
  ' Pack RB+ file
  '
  Print # 10; "    Packing basic binary..."
  System "pack2e.exe " + __argv(1) + " " + __argv(1) + ".pak", Wait, Show SW_HIDE
  '
  ' Now dump packed RB+ file
  '
  Print # 10; "    Dumping packed file..."
  Open __argv(1) + ".pak" for Input As # 2
  rblen% = LOF(# 2)

  BGet # 2, b%, rblen%
  BPut # 1, b%, rblen%
  Add romlen%, rblen%
Else
  BGet # 5, b%, rblen%
  BPut # 1, b%, rblen%
  Add romlen, rblen%
EndIf

'
' Finally, pad the ROM
'

Print # 10; "    Padding ROM..."
Dim pad$
pad$ = String$(8 * 1024 * 1024 / 24, "RB+ (C) Reboot 2014-2017 ")

'CopyFile __argv(1) To __argv(2)
'Print romlen%
'Repeat
'Until InKey$ <> ""

If romlen% < 1024 * 1024
  BPut # 1, V:pad$, 1024 * 1024 - romlen%
ElseIf romlen% < 2 * 1024 * 1024
  BPut # 1, V:pad$, 2 * 1024 * 1024 - romlen%
ElseIf romlen% < 4 * 1024 * 1024
  BPut # 1, V:pad$, 4 * 1024 * 1024 - romlen%
ElseIf romlen% < 6 * 1024 * 1024
  BPut # 1, V:pad$, 6 * 1024 * 1024 - romlen%
ElseIf romlen% < 8 * 1024 * 1024                'Well, 8mb ROM is only possible with skunkboard and bank switching - I suppose it can't hurt to be here!
  BPut # 1, V:pad$, 8 * 1024 * 1024 - romlen%
Else
  Print # 10; "Make-a-rom abort - produced ROM too large!"
  Quit 1
EndIf
Close # 1
Print # 10; "Make-a-rom finished - everything went fine!"
Close # 10
Quit 0

>Procedure lpoke_endian(adr%, value%)
  Poke adr%, Shr%(value%, 24)
  Poke adr% + 1, Shr%(value%, 16) And 255
  Poke adr% + 2, Shr%(value%, 8) And 255
  Poke adr% + 3, value% And 255
Return

  //$Export
  // ****************************************************************************
  // * Parameters.Lib                                                           *
  // * GFA-BASIC 32 Library                                                     *
  // *                                                                          *
  // * Splits the command line into C compatible argv Array and                 *
  // * features Delphi/Borland Pascal compatible ParamXX functions              *
  // *                                                                          *
  // * Maintained by GFA Software Technologies GmbH /                           *
  // * GFA Software Technologies, Inc. <info@gfa.net>                           *
  // *                                                                          *
  // * Feel free to modify and redistribute.                                    *
  // ****************************************************************************

  ' Library Parameters

  /* Erstellen eines C-kompatiblen argv-Arrays aus der Kommandozeile

  // __argc enthdlt nach Aufruf der Prozedur ConvertCMDLine
  // die Anzahl der Kommandozeilenargumente. Ein Argument
  // besteht dabei aus beliebigen Zeichen mit Ausnahme des
  // Leerzeichens/Space, das zur Trennung von Argumenten
  // dient.
  // Sollen Argumente ?ber ein oder mehrere Leerzeichen
  // hinausgehen, m?ssen Anfang und Ende des Argumentes
  // zusdtzlich zu den Leerzeichen durch Anf?hrungszeichen
  // (keine typografischen, sondern ") gekennzeichnet werden.

  // __argv ist ein Array mit den einzelnen Kommandozeilen-
  // argumenten; dabei werden maximal __argmax Argumente
  // verwaltet.

  // __argv(0) ist immer der komplette Pfad inkl. Dateinamen
  // der eigenen Programmdatei.

  //$Export Var __argc "numbers of elements in command line"
  //$Export Var __argv "array of elements in command line"
  //$Export Sub  ConvertCMDLine "()"
  //$Export Func ParamCount "() As Int ' Delphi/Borland Pascal compatible"
  //$Export Func ParamStr "(ByVal Index As Int) ' Delphi/Borland Pascal compatible"

  Global Const __argmax = 50
  Global __argc As Int, __argv() As String

>Procedure ConvertCMDLine()
  Local i As Int = 0, j As Int = 0
  'Print "---" + _DosCmd$ + "---"
  Local cmd$ = Trim(_DosCmd$) + " "
  'cmd$ = "i:\svn\reboot\JiFFI\source>JiFFI_1.3.Exe -i c:\Users\gn\Desktop\UFO.BIN -o ""c:\$WINDOWS.~BT\jag\vj2\software"" -load 10000 -run 12000 -rom -all -overwrite -nyan -yaarrr "
  Local LargeArg As Boolean = False
  Local a$

  ReDim __argv(__argmax)

  __argv(0) = App.FileName
  // Anf?hrungszeichen rund um App.FileName entfernen,
  // falls vorhanden
  If Left$(__argv(0), 1) = #34
    __argv(0) = Mid(__argv(0), 2)
  EndIf
  If Right$(__argv(0), 1) = #34
    __argv(0) = Left(__argv(0), Len(__argv(0)) - 1)
  EndIf
  '##E#

  If Left$(cmd$, 1) <> """"
    i = InStr(cmd$, #32)
  Else
    i = InStr(cmd$, #34, 2) : LargeArg = True
    cmd$ = Mid$(cmd$, 2) // Leerzeichen am Anfang abschneiden
  EndIf

  While i > 0
    j++
    If LargeArg
      // Space und Leerzeichen am Ende abschneiden
      a$ = Left$(cmd$, i - 2)
      If Len(a)
        __argv(j) = Left$(cmd$, i - 2)
      Else
        j--
      EndIf
      LargeArg = False
    Else
      // nur Leerzeichen am Ende abschneiden
      a$ = Left$(cmd$, i - 1)
      If Len(a)
        __argv(j) = Left$(cmd$, i - 1)
      Else
        j--
      EndIf
    EndIf

    Exit If (i + 1) > Len(cmd$)
    cmd$ = Mid$(cmd$, i + 1)

    If Left$(cmd$, 1) <> """"
      i = InStr(cmd$, #32)
    Else
      i = InStr(cmd$, #34, 2) : LargeArg = True
      cmd$ = Mid$(cmd$, 2) // Leerzeichen am Anfang abschneiden
    EndIf
  Wend

  // Anzahl der Argumente zur?ckgeben; ggf. 0
  __argc = j
Return
$DatFile
$FormVersion = 2
:Univ 696, 8192
0KAUDtWMsKAAgcqA//PAH8vAGavQjwTDs2RjwOScT57hRcBmnQgJiEsvfkHMbDpxIPB+JEUm4F1y
/zY+/VH8r5g/T0ChYHS5RSdA/MiXT4q8v8ObJgtBfmzJP22TDMd2E2j6reLSxbOmRdC8uRpDQKJy
0D1OGhFCjA+SSzDHFDzaRQi+GjXKRPlWkBekaZB7ZfIxY3iFYqWhpEw9lsv95H8D4c6sb6hxZkmH
rVQA3BdzJA/pPoqdkIpJo8uckbYQ7MoI8cwTntgtn8FfXzuU2/LRuz5azogN+y06+QD560v91dYs
tbfUyZQmoARcXTkfe/glzTYA/nVczILGV5kVDxkppLn4K9JBaeY8zJph+A4BVxhzXZ30ck8QC23z
Zkkb7nBY0Jn1QeTg4tPEB4X5cdbgIK+6HIadQUTELnuL1lTe+YHu4rFi/z2dH0rzFYv0pMwX+SQU
YffFHkvvZLxJ9Au8ADyL/aqu2tWaPWQQB0knwn3hvy427Dz4JHVKq4Tk6AuUuXk55pVPSKhEfwmY
iuZmsitMzT/zKlgVa1gdEGuW/PSaSPg5uckxUlwoH52WaFK6lUcphRGuCi8FaoIydweSU6BAgOSO
1MvsnS1RQusUAh3ozg46joQKbc8tiDRdfeyWjDwYvJRVgsARye270VxF7u3tViHw+zSmd/P38P0r
CJ8JKOlnitgEOOtX29wKd2WjoQEo0GrIo2JLBAJBr2AYpA21JJAJeCRHmzBWJzPFXK5IeQn3j86p
2utU4ptXsZxa2cBvB8naVdafADW/QJHeiRR/Gc/DeIIz7vgYGgXHAuQVumcJhVGibPiDNsf27E0M
Fi8nhxbyXISKOFkyl+037B/tDT6kyK/5NtjRhIS2Q9pL/tyoSHKJoR+vo8/B+HOdHcABEQABAAII
AAAAAAw/H4/+zDAA
:COPYROM 202, 216
GDAUDtWM+CA2AkLA//PAHokR8fCAzw/f/DA8A4UQ5HkUTV0Q5b0TPtELJBmDgwjUCtSIig9UA6m+
OZdYC4k18Bb6O5HA69PcACmASgN0AQGCmhPEYENAlJvcBQHAQDgZEERDBMdQSgBAWIBGCAxUBdRE
AcADqxNYgdVQlZR4JKBGGFo4BSGGVcRBVLEYwISBVkwAlhuUCZhBAw9FdAAEYJg8mZQELcgGkpOV
CpSAHFPGAIphVfkEbLx2Rp8/8DGA/zmT1BAAAAAAAA==
:Copyrom_gpu 524, 664
IIAUDtWMAIAmCsfAWIgAHC0EvEfivTcISYUEiE54fL//yVBJjgVERMiMhFxEUNvFivE/ZT/WHFfP
REa8fgDAgAgWwAJGCg7gOAYUxFKxMMQECEbAAAEAjGQiEQABMl0JmvpAS4jx/QPoWLdtgCVwBM0F
DJIZGHICXKedCKv2iigNq8jOwrm6M/QIECspMeWSLhZ+hUIFYTZa8Jd2jT4hew/LqRE9Q3nfdODh
DgrC3YC0/8jfQZoF+eSjlWRm+QXoK3hgnZEH6CfnhcoLEFm/UedmMUUTFehZWhecUtd123OWmxco
Hv+0HG2WHexy2ygA/bOOQG2VjvNYlQatJ8N0RbrCChoI0hLkvMU/8h9QvK5G+hed8G6he1yNeD96
5NQD96qQFheVzNwC9KC8B+Qv65GwheFC3IM0rY4+M0JI1t06D6D//ES1l////aIE8HIyKHyBGggj
1JVTukzlJayhdt5AjwkK3BbYVO3dAlDEiEEInLRyfyOHPyhSUCQG4Ns6QQhXLH0c5ZgIt14TqZkM
PQmD7czBNu3R8GATN5DTqsRz3A2i7gNHWN9kEU8u7PHgJlVw1W4BtcoJad+Zzyqx2lmLwGYuC1y1
X6q5FmwAn2C2uolDFkzAicsOZbylklEA0IaHYTu49mD1h4YoTIQ6aaDliUudUCEC1jIASCqlwboX
X2MdQClkwqlIAAA=
:{frm1 65
EAAAAAAAAAAAAAAAWWEAAEWRvAQFmJXbxAgElCAAE20UgMFalxGbgQEbnBAAAAACAAAgPAAAACAA
AEAAHAAgAAA=
$ExeName = ..\BIN\makearom.Exe
$FileDescription = Description
$Comments = Author: Not registered   
$CompanyName = Not registered   
$LegalCopyright = © 2013 Not registered   
$LanguageId = 0409
$FileVersion = %d.%d.%d.%d
$ProductVersion = %d.%d.%d.%d
$DatFile
